// construct-plymouth script file


// status code : 0 for none, 1 for starting, 2 for waiting, 3 for trying, 4 for terminating

// initialise window and background

window.width = Window.GetWidth();
window.height = Window.GetHeight();
origin.x = Window.GetX();
origin.y = Window.GetY();

Window.SetBackgroundTopColor(0, 0.0745, 0.0745);
Window.SetBackgroundBottomColor(0, 0.0745, 0.0745);

background.image = Image("background.png");
background.sprite = Sprite(background.image);
background.x = origin.x + ((window.width - background.image.GetWidth()) / 2);
background.y = origin.y + ((window.height - background.image.GetHeight()) / 2);
background.sprite.SetPosition(background.x, background.y, 0);

// STATUS VARIABLES

dot.status = 1;
dot.clear = 0;
dot.timer = 0;
passwordbox.status = 0;

// DEBUG PARAMS

debug_time = 0;
debug_progress = 0;
debug_text = "none";
debug_frames = 0;
status_display = Sprite();
status_display.SetPosition(background.x + 50, background.y + 100, 10);

// INITIALISE DOT MATRIX

for (rot = 0; rot < 4; rot++)
{
    rot_radians = rot * (3.14159265 / 2.0);
    tick_image_rotated = Image("tick0.png").Rotate(rot_radians);
    for (i = 0; i < 3; i++)
    {
        for (j = 0; j < 3; j++)
        {
            tick_x = 2*i;
            if (rot == 2 || rot == 3)
            {
                tick_x += 1;
            }
            tick_y = 2*j;
            if (rot == 1 || rot == 2)
            {
                tick_y += 1;
            }
            dot.matrix[tick_x][tick_y].sprite = Sprite(tick_image_rotated);
            dot.matrix[tick_x][tick_y].anim_index = 0;
            dot.matrix[tick_x][tick_y].rot = rot;
            if ((tick_x == 1 || tick_x == 4) && (tick_y == 1 || tick_y == 4))
            {
                dot.matrix[tick_x][tick_y].sprite.SetImage(Image("tick49.png").Rotate(rot_radians));
                dot.matrix[tick_x][tick_y].anim_index = 49;
            }
            dot.matrix[tick_x][tick_y].sprite.SetPosition(background.x + 1145 + (tick_x*90) , background.y + 275 + (tick_y*90), 5);
        }
    }
}

fun update_boot_progress(boot_time, boot_progress)
{
    debug_time = boot_time;
    debug_progress = boot_progress;
}
Plymouth.SetBootProgressFunction(update_boot_progress);

fun update_debug_text(status_text)
{
    debug_text = status_text;
    dot.status = 4;
    passwordbox.status = 4;
    dot.status = 4;
    dot.clear = 0;
    dot.timer = 0;
}

// called when crypt password is entered correctly and plymouth proceeds to boot
Plymouth.SetUpdateStatusFunction(update_debug_text);

fun clear_dot_matrix()
{
    not_clear_count = 0;
    for (i = 0; i < 6; i++)
    {
        for (j = 1; j < 5; j++)
        {
            if (dot.timer - (i*j) >= 0)
            {
                deactivate_dot_matrix_tick(i, j);
            }
            if (dot.matrix[i][j].anim_index > 0)
            {
                not_clear_count++;
            }
        }
    }
    if (not_clear_count == 0)
    {
        dot.clear = 1;
        dot.timer = 0;
    }
}

fun activate_dot_matrix_tick(x, y)
{
    if (dot.matrix[x][y].anim_index >= 0 && dot.matrix[x][y].anim_index < 49)
    {
        dot.matrix[x][y].anim_index++;
        dot.matrix[x][y].sprite.SetImage(Image("tick" + dot.matrix[x][y].anim_index + ".png").Rotate(dot.matrix[x][y].rot * 3.14159265 / 2));
    }
}

fun deactivate_dot_matrix_tick(x, y)
{
    if (dot.matrix[x][y].anim_index > 0 && dot.matrix[x][y].anim_index <= 49)
    {
        dot.matrix[x][y].anim_index--;
        dot.matrix[x][y].sprite.SetImage(Image("tick" + dot.matrix[x][y].anim_index + ".png").Rotate(dot.matrix[x][y].rot * 3.14159265 / 2));
    }
}

fun animation()
{
    // DEBUG INFO
    global.debug_frames++;
    global.debug_image = Image.Text(global.debug_text + " | " + global.debug_progress + " | " + global.debug_time + " | " + global.debug_frames, 1, 1, 1);
    global.debug.sprite = Sprite(global.debug_image);
    global.debug.sprite.SetPosition(background.x + 50, background.y + 50, 10);
    status_display_image = Image.Text(dot.status + " | " + passwordbox.status, 1, 1, 1);   
    status_display.SetImage(status_display_image);

    // DOT STARTING ANIMATION : deactivate ticks left over from construct_grub
    if (dot.clear == 0)
    {
        dot.timer++;
        clear_dot_matrix();
    }
    else if (dot.status == 1)
    {
        dot.status = 2;
        dot.timer = 0;
    }
    // DOT WAITING ANIMATION : activate waiting ticks
    else if (dot.status == 2)
    {
        dot.timer++;
        for (i = 0; i < 6; i++)
        {
            for (j = 2; j < 4; j++)
            {
                if (dot.timer - (4*i*(j-2)) >= 0)
                {
                    activate_dot_matrix_tick(i, j);
                }
            }
        }
    }

    // DOT TRYING AND TERMINATION ANIMATION : deactivate waiting ticks

    // PASSWORDBOX ANIMATION

    if (passwordbox.status == 1)
    {
        frame = Image("passwordbox_starting" + passwordbox.anim_index + ".png");
        passwordbox.sprite.SetImage(frame);
        passwordbox.anim_index++;
        if (passwordbox.anim_index > 49)
        {
            passwordbox.status = 2;
            passwordbox.anim_index = 0;
        }
    }
    else if (passwordbox.status == 4)
    {
        frame = Image("passwordbox_terminating" + passwordbox.anim_index + ".png");
        passwordbox.sprite.SetImage(frame);
        passwordbox.anim_index++;
        if (passwordbox.anim_index > 49)
        {
            passwordbox.status = 0;
        }
        bullet_opacity = 1.0 - (0.0204 * passwordbox.anim_index);
        for (i = 0; passwordbox.bullet[i] ; i++)
        {
            passwordbox.bullet[i].sprite.SetOpacity(bullet_opacity);
        }
    }
}

Plymouth.SetRefreshFunction(animation);

fun display_password_box(prompt, bullets)
{
    if (dot.status == 3)
    {
        dot.status = 2;
        dot.clear = 0;
        dot.timer = 0;
    }
    if (passwordbox.status == 0)
    {
        passwordbox.x = 245 + background.x;
        passwordbox.y = 424 + background.y;
        passwordbox.z = 1;
        passwordbox.sprite = Sprite();
        passwordbox.sprite.SetPosition(passwordbox.x, passwordbox.y, passwordbox.z);
        passwordbox.anim_index = 0;
        passwordbox.bullet_image = Image("bullet.png");
        passwordbox.status = 1;
    }
    for (i = 0; ( i < bullets || passwordbox.bullet[i]) && i < 38; i++)
    {
        if (!passwordbox.bullet[i])
        {
            passwordbox.bullet[i].sprite = Sprite(passwordbox.bullet_image);
            passwordbox.bullet[i].x = passwordbox.x + 136 + (12 * i);
            passwordbox.bullet[i].y = passwordbox.y + 109;
            passwordbox.bullet[i].z = passwordbox.z + 1;
            passwordbox.bullet[i].sprite.SetPosition(passwordbox.bullet[i].x, passwordbox.bullet[i].y, passwordbox.bullet[i].z);
        }
        if (i < bullets)
            passwordbox.bullet[i].sprite.SetOpacity(1);
        else
            passwordbox.bullet[i].sprite.SetOpacity(0);
    }
}

Plymouth.SetDisplayPasswordFunction(display_password_box);

fun reset_password_box()
{
    if (dot.status != 1) 
    {
        dot.status = 3;
        dot.clear = 0;
        dot.timer = 0;
    }
}
// called the moment ENTER is pressed
Plymouth.SetDisplayNormalFunction(reset_password_box);